prototype(Neos.DocsNeosIo:Content.Navigation) < prototype(Neos.Neos:ContentComponent) {
    id = Neos.DocsNeosIo:Helper.StringToHash {
        value = ${q(node).property('title') || node.identifier}
    }
    title = Neos.Neos:Editable {
        property = 'title'
        block = false
        @process.icons = Neos.DocsNeosIo:Helper.IconProcessor
    }
    headlineSize = ${q(node).property('headlineSize')}

    @context.navRoot = ${q(node).property('navReference') || documentNode}

    navigation = Neos.DocsNeosIo:Component.Sidebar {
        startingPoint = ${navRoot}
        itemClass.base = 'collection-item'
        @cache.entryIdentifier.node = ${node}
    }

    navItems = ${q(navRoot).children('[instanceof Neos.Neos:Document][_hidden != TRUE][_hiddenInIndex != TRUE][hideFromMainMenu != TRUE]')}

    renderer = afx`
        <div id={props.id} class="content-navigation">
            <Neos.Fusion:Tag tagName={props.headlineSize} @if={props.title || node.context.inBackend} class="header">
                <a href={'#' + props.id} class="header-anchor" aria-label={props.title}>#</a>
                {props.title}
            </Neos.Fusion:Tag>
            <ul class="link-list collection">
                <Neos.Fusion:Loop items={props.navItems} itemName="node">
                    <li class="collection-item">
                        <Carbon.Link:Link node={node} additionalClass="collection-item__link"
                                          renderDefaultTagIfNoLink={true} defaultTagName="p">
                            <span class="icon-wrapper"><span class="inline-icon fas fa-fw fa-level-up-alt"></span></span>
                            <span class="collection-item__text">{q(node).property('mainMenuTitle') || q(node).property('titleOverride') || q(node).property('title')}</span>
                            <span class="icon-wrapper"><span class="inline-icon fas fa-chevron-right"></span></span>
                        </Carbon.Link:Link>
                    </li>
                </Neos.Fusion:Loop>
            </ul>
        </div>
    `
}
