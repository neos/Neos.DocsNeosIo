prototype(Neos.DocsNeosIo:Content.LinkList.Item) < prototype(Neos.Neos:ContentComponent) {
    title = Neos.Neos:Editable {
        property = 'title'
        block = false
        @process.icons = Neos.DocsNeosIo:Helper.IconProcessor
    }
    link = ${q(node).property('link')}
    iconClass = ${q(node).property('iconCustom') || q(node).property('icon')}

    renderer = afx`
        <li class="collection-item">
            <Carbon.Link:Link
                @if={props.title}
                link={props.link}
                renderDefaultTagIfNoLink={true}
                additionalClass="collection-item__link"
                defaultTagName="div"
            >
                {props.title}
                <Neos.DocsNeosIo:Component.Icon
                    iconClass={props.iconClass}
                    fromLinkTypeUri={props.link}
                    additionalClass="right inline-icon"
                />
            </Carbon.Link:Link>
        </li>
    `
}
