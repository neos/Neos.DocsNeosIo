prototype(Neos.DocsNeosIo:Document.Tag) >
prototype(Neos.DocsNeosIo:Document.Tag) < prototype(Neos.Fusion:Case) {
    inBackend {
        condition = ${renderingMode.isEdit}
        renderer = Neos.DocsNeosIo:Document.AbstractPage {
            body.subtitle = 'You can define a notice content below. This notice will be displayed on all tagged pages.'
            body.content = Neos.DocsNeosIo:Component.Notice {
                type = ${q(node).property('noticeType')}
                title = Neos.Neos:Editable {
                    property = 'noticeTitle'
                }
                content = Neos.Neos:Editable {
                    property = 'noticeText'
                }
            }
        }
    }
    default {
        condition = true
        renderer = Neos.DocsNeosIo:Document.Page {
            httpResponseHead.statusCode = 404
            documentNode = ${q(site).children('[uriPathSegment = "404"]').get(0)}
            @context.documentNode = ${this.documentNode}
            @context.node = ${this.documentNode}
        }
    }
}
