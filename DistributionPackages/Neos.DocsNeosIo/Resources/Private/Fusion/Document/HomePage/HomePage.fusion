prototype(Neos.DocsNeosIo:Document.HomePage) < prototype(Neos.DocsNeosIo:Document.AbstractPage) {
    bodyTag.attributes.class.@process.setNoSidebar = ${Array.push(value, 'no-sidebar')}
    body = Neos.DocsNeosIo:Component.Layout {
        navCollection = ${q(node).find('main').children()}
        showSupportCall2Action = false

        sidebarOnlyMobile = true
        localNav = false

        banner = Neos.Fusion:Component {
            logo = Neos.Fusion:ResourceUri {
                path = "resource://Neos.DocsNeosIo/Public/Frontend/img/neos-logo-light.svg"
            }

            firstButtonLink = ${q(node).property('firstButtonLink')}
            secondButtonLink = ${q(node).property('secondButtonLink')}

            renderer = afx`
                <div class="banner">
                    <img src={props.logo} width="400" height="110" alt="Neos" />
                    <h1 style="font-size: 2rem">Developer Documentation</h1>
                    <div @if={props.firstButtonLink || props.secondButtonLink} class="cta-buttons">
                        <Carbon.Link:Link node={props.firstButtonLink} additionalClass="btn__wrapper"><div class="btn secondary"><Neos.Neos:Editable property="firstButtonText" /></div></Carbon.Link:Link>
                        <Carbon.Link:Link node={props.secondButtonLink} additionalClass="btn__wrapper"><div class="btn secondary"><Neos.Neos:Editable property="secondButtonText" /></div></Carbon.Link:Link>
                    </div>
                </div>
            `
        }

        content = Neos.Fusion:Component {
            main = Neos.Neos:ContentCollection {
                nodePath = 'main'
            }

            renderer = afx`
                {props.main}
            `
        }
    }
}
