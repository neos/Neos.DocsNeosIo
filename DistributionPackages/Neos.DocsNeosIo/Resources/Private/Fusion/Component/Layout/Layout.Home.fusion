prototype(Neos.DocsNeosIo:Component.Layout.Home) < prototype(Neos.Neos:ContentComponent) {
    authors = false
    title = false
    subtitle = false
    content = ''
    navCollection = false
    fullwidth = false
    tags = false
    navigationRoot = false
    showSupportCall2Action = true

    @context.tags = ${this.tags}
    tagNotices = Neos.Fusion:Collection {
        collection = ${tags}
        itemName = 'node'
        itemRenderer = Neos.DocsNeosIo:Component.Notice {
            @if.hasTitle = ${String.trim(q(node).property('noticeTitle'))}
            type = ${q(node).property('noticeType')}
            title = ${q(node).property('noticeTitle')}
            content = ${q(node).property('noticeText')}
        }
    }
    prevNextNavigation = Neos.DocsNeosIo:Component.PrevNextNavigation {
        @if.showPrevNextNavigation = ${q(node).property('showPrevNextNavigation')}
        prevNode = ${q(documentNode).prevAll('[instanceof Neos.Neos:Document][_hidden != TRUE][_hiddenInIndex != TRUE][hideFromMainMenu != TRUE]').last().get(0)}
        nextNode = ${q(documentNode).nextAll('[instanceof Neos.Neos:Document][_hidden != TRUE][_hiddenInIndex != TRUE][hideFromMainMenu != TRUE]').get(0)}

        @cache {
            mode = 'cached'
            entryIdentifier.node = ${documentNode}
            entryTags {
                1 = ${Neos.Caching.nodeTypeTag('Neos.Neos:Document', documentNode)}
            }
        }
    }

    logo = Neos.Fusion:ResourceUri {
        path = "resource://Neos.DocsNeosIo/Public/Frontend/img/neos-logo-light.svg"
    }

    renderer = afx`
        <Neos.DocsNeosIo:Component.Header startingPoint={props.navbarRoot} node={props.navbarRoot}
                                          navigationRoot={props.sidebarRoot}
                                          renderCurrentPath={true}
                                          sidebarOnlyMobile={true}
        />
        <main class="home">
            <div class="banner">
                <img src={props.logo} width="400" height="110" alt="Neos" />
                <h1 style="font-size: 2rem">Developer Documentation</h1>
                <div style="margin-top: 20px;display: grid; grid-template-columns: 1fr 1fr; gap: 20px">
                    <div class="btn__wrapper"><Carbon.Link:Link link="/guide/getting-started" additionalClass="btn secondary">Get started</Carbon.Link:Link></div>
                    <div class="btn__wrapper"><Carbon.Link:Link link="/guide/installation-development-setup" additionalClass="btn secondary">Install</Carbon.Link:Link></div>
                </div>
            </div>

            <div class="container fullwidth-layout" @if.is={props.fullwidth}>
                {props.tagNotices}
                {props.content}

                <Neos.DocsNeosIo:Component.Authors authors={props.authors} />
            </div>
            <div class="spacing-wrapper" @if.isNot={!props.fullwidth}>
                {props.tagNotices}
                {props.content}
                {props.prevNextNavigation}
                <Neos.DocsNeosIo:Component.Authors authors={props.authors} />
            </div>
        </main>
        <Neos.DocsNeosIo:Component.Footer />
    `
}
