prototype(Neos.DocsNeosIo:Component.Layout.Default) < prototype(Neos.Neos:ContentComponent) {
    authors = false
    title = false
    subtitle = false
    content = ''
    navCollection = false
    fullwidth = false
    tags = false
    navigationRoot = false
    showSupportCall2Action = true

    @context.tags = ${this.tags}
    tagNotices = Neos.Fusion:Collection {
        collection = ${tags}
        itemName = 'node'
        itemRenderer = Neos.DocsNeosIo:Component.Notice {
            @if.hasTitle = ${String.trim(q(node).property('noticeTitle'))}
            type = ${q(node).property('noticeType')}
            title = ${q(node).property('noticeTitle')}
            content = ${q(node).property('noticeText')}
        }
    }
    prevNextNavigation = Neos.DocsNeosIo:Component.PrevNextNavigation {
        @if.showPrevNextNavigation = ${q(node).property('showPrevNextNavigation')}
        prevNode = ${q(documentNode).prevAll('[instanceof Neos.Neos:Document][_hidden != TRUE][_hiddenInIndex != TRUE][hideFromMainMenu != TRUE]').last().get(0)}
        nextNode = ${q(documentNode).nextAll('[instanceof Neos.Neos:Document][_hidden != TRUE][_hiddenInIndex != TRUE][hideFromMainMenu != TRUE]').get(0)}

        @cache {
            mode = 'cached'
            entryIdentifier.node = ${documentNode}
            entryTags {
                1 = ${Neos.Caching.nodeTypeTag('Neos.Neos:Document', documentNode)}
            }
        }
    }

    renderer = afx`
        <Neos.DocsNeosIo:Component.TopNavigation />
        <Neos.DocsNeosIo:Component.Header navigationRoot={props.navigationRoot} />
        <main>
            <div class="section" id="index-banner">
                <div class="container">
                    <div class="row" style="margin-bottom: 0;" @if.withoutOneAuthor={!props.authors || Array.length(props.authors) != 1}>
                        <div class="col s12 m11 offset-m1 xl8 offset-xl1 center-on-small-only">
                            <h1 class="header">{props.title}</h1>
                            <h4 class="light" @if.has={props.subtitle || node.context.inBackend}>{props.subtitle}</h4>
                            <Neos.DocsNeosIo:Component.TagList collection={props.tags} additionalClass="backend-tags" filter="" @if.inBackend={node.context.inBackend} />
                        </div>
                    </div>
                    <div class="row" style="margin-bottom: 0;" @if.hasOneAuthor={props.authors && Array.length(props.authors) == 1}>
                        <div class="col s12 m8 offset-m1 xl7 offset-xl1 center-on-small-only">
                            <h1 class="header">{props.title}</h1>
                            <h4 class="light" @if.has={props.subtitle || node.context.inBackend}>{props.subtitle}</h4>
                        </div>
                        <div class="col s12 m3 center">
                            <div class="image-container">
                                <img src={Array.first(props.authors).gravatar} class="profile-image" style="margin-top: 16px;"/><br />
                                <i>Written by</i><br/>
                                {Array.first(props.authors).label}
                            </div>
                        </div>
                    </div>
                </div>
            </div>

            <div class="support-call2action col s12" @if.show={props.showSupportCall2Action}>
                <h2>Support the documentation</h2>
                <p>This documentation was written by volunteers. Please help support our effort:</p>
                <a class="btn" href="mailto:roland@neos.io?subject=Contributing%20to%20Neos%20Docs&amp;body=Hello%2C%0A%0Aplease%20create%20a%20Neos%20Docs%20account%20for%20myself%2C%20so%20I%20can%20improve%20the%20documentation.%0A%0ABest%20regards"><div class="btn__content">Help us improve the documentation</div></a>
            </div>

            <div class="container fullwidth-layout" @if.is={props.fullwidth}>
                {props.tagNotices}
                {props.content}

                <Neos.DocsNeosIo:Component.Authors authors={props.authors} />
            </div>
            <div class="container" @if.isNot={!props.fullwidth}>
                <div class="row">
                    <div class="col s12 m8 offset-m1 xl7 offset-xl1">
                        {props.tagNotices}
                        {props.content}
                        {props.prevNextNavigation}
                        <Neos.DocsNeosIo:Component.Authors authors={props.authors} />
                    </div>
                    <div @if.has={props.navCollection} class="col hide-on-small-only m3 xl3 offset-xl1">
                        <Neos.DocsNeosIo:Component.LocalNavigation collection={props.navCollection} />
                    </div>
                </div>
            </div>
        </main>
        <Neos.DocsNeosIo:Component.Footer />
    `
}
