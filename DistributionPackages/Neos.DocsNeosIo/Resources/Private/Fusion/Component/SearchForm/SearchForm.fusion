prototype(Neos.DocsNeosIo:Component.SearchForm) < prototype(Neos.Neos:ContentComponent) {

    class = null

    ajaxTarget = false

    # See Flowpack.SearchPlugin:Search.Form
    searchWord = ${request.arguments.search}
    ajaxSearchUri = Neos.Fusion:UriBuilder {
        package = 'Flowpack.SearchPlugin'
        controller = 'AjaxSearch'
        action = 'search'
        format = 'json'
        absolute = true
        arguments = Neos.Fusion:DataStructure {
            node = ${documentNode}
        }
    }

    searchResultUri = Neos.Neos:NodeUri {
        node = ${q(site).find('[instanceof Neos.DocsNeosIo:Document.SearchResultsPage]').get(0)}
    }

    renderer = afx`
        <div class={["search-form__wrapper", props.class]}>
            <form method="GET" action={props.searchResultUri} class="search-form">
                <input class="search-input" type="search" name="search" placeholder="Search" value={props.searchWord}
                       autocomplete="off" data-ajax-target={props.ajaxTarget}
                       data-ajax-search-uri={props.ajaxSearchUri} />
                <button type="submit"><span class="sr-only">Search</span>
                    <i class="fas fa-search fa-fw"></i>
                </button>
            </form>
        </div>
    `
}
