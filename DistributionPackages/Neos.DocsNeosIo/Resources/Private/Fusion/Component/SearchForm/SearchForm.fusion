prototype(Neos.DocsNeosIo:Component.SearchForm) < prototype(Neos.Neos:ContentComponent) {

    class = null

    ajaxTarget = false

    # See Flowpack.SearchPlugin:Search.Form
    searchWord = ${request.arguments.search}
    ajaxSearchUri = Neos.Fusion:UriBuilder {
        package = 'Flowpack.SearchPlugin'
        controller = 'AjaxSearch'
        action = 'search'
        format = 'json'
        absolute = true
        arguments = Neos.Fusion:RawArray {
            node = ${documentNode}
        }
    }

    searchResultUri = Neos.Neos:NodeUri {
        node = ${q(site).find('[instanceof Neos.DocsNeosIo:Document.SearchResultsPage]').get(0)}
    }

    renderer = afx`
        <div class={["search-form__wrapper", props.class]}>
            <form method="GET" action={props.searchResultUri} class="search-form">
                <input class="search-input" type="search" name="search" placeholder="Search" value={props.searchWord}
                       autocomplete="off" data-ajax-target={props.ajaxTarget}
                       data-ajax-search-uri={props.ajaxSearchUri} />
                <button type="submit"><span class="sr-only">Search</span>
                    <svg xmlns="http://www.w3.org/2000/svg" width="1em" height="1em" viewBox="0 0 30 30">
                        <path d="M20.7 19.3a10 10 0 10-1.4 1.4l6 6a1 1 0 101.4-1.4zm-7.7 1.7a8 8 0 11.01 0" />
                    </svg>
                </button>
            </form>
        </div>
    `
}
