prototype(Neos.DocsNeosIo:Component.Header) < prototype(Neos.Fusion:Component) {
    homeLink = Neos.Neos:NodeUri {
        node = ${site}
    }

    sidebarOnlyMobile = false

    logo = Neos.Fusion:ResourceUri {
        path = "resource://Neos.DocsNeosIo/Public/Frontend/img/neos-logo-small.svg"
    }

    # rendering
    @context {
        startingPoint = ${this.startingPoint}
        node = ${this.startingPoint}
        renderCurrentPath = ${this.renderCurrentPath}
    }

    renderer = afx`
    <header>
        <div class="main-header">
            <aside class={['sidebar', props.sidebarOnlyMobile && 'sidebar-only-mobile']} tabindex="-1">
                <Neos.DocsNeosIo:Component.NavBarContent
                    startingPoint={props.startingPoint} node={props.startingPoint}
                    renderCurrentPath={props.renderCurrentPath} />
                <ul class="sidebar-links">
                    <Neos.DocsNeosIo:Component.Sidebar startingPoint={props.navigationRoot} renderCurrentPath={true} />
                </ul>
            </aside>
            <div class="sidebar-mobile-backdrop"></div>
        </div>

        <div class="nav-bar">
            <button class="sidebar-button" aria-haspopup="menu" aria-expanded="false" aria-label="open menu">
                <svg xmlns="http://www.w3.org/2000/svg" width="24" height="24" class="menu-open" aria-hidden="true">
                    <path d="M2 5v2h20V5H2zm0 6v2h20v-2H2zm0 6v2h20v-2H2z"/>
                </svg>
                <svg xmlns="http://www.w3.org/2000/svg" width="24" height="24" class="menu-close" aria-hidden="true">
                    <path d="M4.7 3.3 3.3 4.7l7.3 7.3-7.3 7.3 1.4 1.4 7.3-7.3 7.3 7.3 1.4-1.4-7.3-7.3 7.3-7.3-1.4-1.4-7.3 7.3-7.3-7.3z"/>
                </svg>
            </button>
            <Carbon.Link:Link
                node={site}
                additionalClass="nav-bar-title"
                aria-label="Neos Dev Docs, back to home"
            ><img class="logo" src={props.logo} alt="Logo" width="32" />Neos Dev Docs
            </Carbon.Link:Link>
            <Neos.DocsNeosIo:Component.NavBarContent
                startingPoint={props.startingPoint} node={props.startingPoint}
                renderCurrentPath={props.renderCurrentPath} />
        </div>
    </header>
    `

    @cache {
        mode = 'cached'
        entryIdentifier {
            documentNode = ${Neos.Caching.entryIdentifierForNode(documentNode)}
        }
        entryTags {
            1 = ${Neos.Caching.nodeTypeTag('Neos.Neos:Document', documentNode)}
        }
    }
}
