prototype(Neos.DocsNeosIo:Component.LocalNavigation.LevelRenderer) < prototype(Neos.Fusion:Component) {
    attributes = Neos.Fusion:Attributes
    collection = false

    # rendering
    @context.collection = ${this.collection && this.collection.filter('[instanceof Neos.DocsNeosIo:Constraint.Content.LocalNavigation.Item]')}
    @context.nodeDepth = ${this.node.depth - startingPoint.depth}
    items = Neos.Fusion:Collection {
        collection = ${collection}
        itemName = 'node'
        itemRenderer = Neos.Fusion:Component {
            id = Neos.DocsNeosIo:Helper.StringToHash {
                value = ${q(node).property('title') || node.identifier}
            }
            title = ${q(node).property('title')}
            children = ${q(node).children()}
            renderer = afx`
                <li>
                    <a @if.has={props.title} href={'#' + props.id}>
                        {props.title}
                    </a>
                </li>
            `
            @process.icons = Neos.DocsNeosIo:Helper.IconProcessor
        }
    }
    renderer = afx`
        <h3 class="h4" @if={collection.count() > 0}>On this page</h3>
        <Neos.Fusion:Tag tagName="ul" attributes={props.attributes} @if.notEmpty={collection.count() > 0}>
            {props.items}
        </Neos.Fusion:Tag>
    `
}
