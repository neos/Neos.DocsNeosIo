prototype(Neos.DocsNeosIo:Component.Notice) < prototype(Neos.Neos:ContentComponent) {
    id = false
    title = false
    content = false
    type = 'todo'

    iconClass = ${this.type}
    iconClass.@process {
        info = ${value == 'info' ? 'fas fa-info-circle' : value}
        warning = ${value == 'warning' ? 'fas fa-exclamation-circle' : value}
        danger = ${value == 'danger' ? 'fas fa-exclamation-triangle' : value}
        tip = ${value == 'tip' ? 'fas fa-lightbulb' : value}
        todo = ${value == 'todo' ? 'fas fa-list' : value}
        backend = ${value == 'backend' ? 'fas fa-edit' : value}
    }
    renderer = afx`
        <div id={props.id} class={'custom-block ' + props.type} @if.isFrontendNotice={props.type != 'backend' || renderingMode.isEdit}>
            <p @if={props.title || renderingMode.isEdit} class="custom-block-title">
                <Neos.DocsNeosIo:Component.Icon iconName={String.firstLetterToUpperCase(props.type)} iconClass={props.iconClass} additionalClass="inline-icon custom-block-icon" />
                <span @if.backend={props.type == 'backend'}>Backend only: </span>
                {props.title}
            </p>
            {props.content}
        </div>
    `
}
