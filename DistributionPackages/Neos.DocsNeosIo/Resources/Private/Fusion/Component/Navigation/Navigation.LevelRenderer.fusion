prototype(Neos.DocsNeosIo:Component.Navigation.LevelRenderer) < prototype(Neos.Fusion:Component) {
	@context.nodeDepth = ${this.node.depth - startingPoint.depth}
	node = ${node}
	itemTitle = ${q(node).property('titleOverride') || q(node).property('title')}
    itemTitle.@process.replaceShy = Carbon.Hyphen:Shy.AlwaysReplace
    itemStatus = ${q(node).property('status')}
	itemClass = Neos.Fusion:RawArray {
		base = 'navigation-item bold'
		level = ${'navigation-item--level-' + nodeDepth}
		current = 'current'
		current.@if.isCurrent = ${node == documentNode}
		active = 'active'
		active.@if.isActive = ${node == documentNode || (nodeDepth != 1 && (q(documentNode).parents('[_identifier = "' + node.identifier + '"]').count() > 0))}
	}
	navigationClass = ${'navigation-level navigation-level--level-' + (nodeDepth + 1)}
	navigationItems = Neos.Fusion:Collection {
		@if.notEmpty = ${this.collection.count() > 0}
		collection = ${q(node).children('[instanceof Neos.Neos:Document][_hiddenInIndex != TRUE][hideFromMainMenu != TRUE]')}
		itemName = 'node'
		itemRenderer = Neos.DocsNeosIo:Component.Navigation.LevelRenderer
	}

	renderNextLevel = ${(node == documentNode || ((q(documentNode).closest('[_identifier = "' + node.identifier + '"]').count() > 0))) && this.navigationItems}

	renderer = afx`
		<li class={props.itemClass} >
			<Carbon.Link:Link title={props.itemTitle} node={node} additionalClass="navigation-link" backendLink={true}>
                {props.itemTitle}
                <span @if.has={props.itemStatus} data-badge-caption={props.itemStatus} class={'badge new status-' + props.itemStatus}></span>
            </Carbon.Link:Link>
			<ul @if.hasNextLevel={props.renderNextLevel} class={props.navigationClass}>
				{props.navigationItems}
			</ul>
		</li>
	`
}
